plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'com.github.davidmc24.gradle.plugin.avro' version '1.3.0' // Avro 플러그인

}

group = 'org.example'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.apache.kafka:kafka-streams'
	implementation 'org.springframework.kafka:spring-kafka'

	// https://mvnrepository.com/artifact/org.apache.avro/avro
	implementation 'org.apache.avro:avro:1.11.3'


	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.kafka:spring-kafka-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

// Git에서 스키마 파일을 체크아웃하기 위한 테스크
task checkoutSchema {
	doLast {
		File avroDir = file("${projectDir}/src/main/avro")
		if (avroDir.exists()) {
			// 디렉터리가 존재하면 pull로 업데이트
			exec {
				workingDir avroDir
				commandLine 'git', 'pull'
			}
		} else {
			// 디렉터리가 없으면 클론
			exec {
				commandLine 'git', 'clone', 'https://github.com/zzangisdaeho/avro-schema-sample', avroDir
			}
		}
	}
}

// checkoutSchema 테스크가 실행된 후에 Avro 소스를 생성
tasks.named('generateAvroJava') {
	dependsOn checkoutSchema
}

// Avro 플러그인 설정
avro {
	createSetters = true // setter 메소드 생성 여부
	fieldVisibility = 'PRIVATE' // 필드 가시성 설정
}

sourceSets {
	main {
		java {
			srcDir 'build/generated-main-avro-java'
		}
	}
}
